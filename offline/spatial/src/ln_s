#!/bin/csh
#=======================================================================
# name - ln_s
# purpose - link nc4 input file, or convert associated bin input to nc4
#
# input parameter
# => nc4_root: path name for nc4 input file that is needed for plotting
#
# !REVISION HISTORY:
#
# 15Dec2021   jstassi     Initial version of script
#=======================================================================
unalias rm

# check for FVROOT definition
#----------------------------
if (! $?FVROOT) then
#setenv FVROOT /home/dao_ops/GEOSadas-5_29_3/GEOSadas/install
    setenv FVROOT /home/dao_ops/GEOSadas-5.29.4-p4-SLES15/GEOSadas/install-SLES15
endif

# get input; remove extension, if already included
#-------------------------------------------------
set nc4_root = $1
if ($nc4_root:e == "bin") set nc4_root = $nc4_root:r
if ($nc4_root:e == "nc4") set nc4_root = $nc4_root:r

set nc4_input = $nc4_root.nc4
set nc4_local = $nc4_input:t

set bin_input = $nc4_root.bin
set bin_local = $bin_input:t

# remove previously existing local input
#---------------------------------------
if (-e $nc4_local) rm $nc4_local
if (-e $bin_local) rm $bin_local
set echo

#------------------------------------------------
# if .nc4 input is present, then link it,
# or if .bin input present, then convert to .nc4,
# or warn if cannot find nc4 or bin input file
#------------------------------------------------
if (-e $nc4_input) then
   ln -s $nc4_input .

else if (-e $bin_input) then
   ln -s $bin_input .
   $FVROOT/bin/gsidiag_rad_bin2nc4.x -npred 12 $bin_local
   rm $bin_local

else
   unset echo
   echo
   echo "Warning. Cannot find nc4 or bin input"
   echo "$nc4_input (NOT FOUND)"
   echo "$bin_input (NOT FOUND)"
   echo
   exit 999
endif
unset echo
